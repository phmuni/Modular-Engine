#version 330 core
out vec4 FragColor;

struct Material {
    sampler2D diffuse;
    sampler2D specular;
    sampler2D normal;    // normal map
    sampler2D emission;
    float shininess;
}; 

struct Light {
    vec3 position;
    vec3 direction;
    vec3 color; 
    float intensity;
    float ambient;
    float constant;
    float linear;
    float quadratic;
    float cutOff;
    float outerCutOff;
    int type; // 0 = directional, 1 = point, 2 = spotlight
};

in vec3 FragPos;  
in vec3 Normal;  
in vec2 TexCoords;

uniform vec3 viewPos;
uniform Material material;
uniform int numLights;
uniform Light lights[10];

// Shadow mapping
uniform sampler2D shadowMap;
uniform mat4 lightSpaceMatrix;
uniform int useShadows;

// direção da luz que gerou o shadow map
uniform vec3 shadowLightDir;

float calculateShadow(vec4 fragPosLightSpace)
{
    if (useShadows == 0) return 0.0;

    vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    projCoords = projCoords * 0.5 + 0.5;

    if (projCoords.z > 1.0) return 0.0;

    float currentDepth = projCoords.z;

    // aqui usa a normal geométrica para o bias (mais estável)
    vec3 nGeom   = normalize(Normal);
    vec3 lightDir = normalize(-shadowLightDir);

    float bias = max(0.01 * (1.0 - dot(nGeom, lightDir)), 0.001);

    float shadow = 0.0;
    vec2 texelSize = 1.0 / textureSize(shadowMap, 0);

    int kernelSize = 3;
    int sampleCount = 0;

    for (int x = -kernelSize; x <= kernelSize; ++x) {
        for (int y = -kernelSize; y <= kernelSize; ++y) {
            vec2 offset = projCoords.xy + vec2(x, y) * texelSize;

            if (offset.x < 0.0 || offset.x > 1.0 ||
                offset.y < 0.0 || offset.y > 1.0)
                continue;

            float pcfDepth = texture(shadowMap, offset).r;

            shadow += (currentDepth - bias > pcfDepth) ? 1.0 : 0.0;
            sampleCount++;
        }
    }

    if (sampleCount > 0)
        shadow /= float(sampleCount);

    return shadow;
}

void main()
{
    // Normal da malha (geométrica)
    vec3 nGeom = normalize(Normal);

    // Normal do normal map (tangent-like, mas ainda sem TBN)
    vec3 nMap = texture(material.normal, TexCoords).rgb;
    nMap = normalize(nMap * 2.0 - 1.0); // [0,1] -> [-1,1]

    // Mistura das duas para evitar reflexos quebrados quando o mapa é default
    float normalStrength = 0.5; // ajuste fino aqui (0 = só geometria, 1 = só normal map)
    vec3 normal = normalize(mix(nGeom, nMap, normalStrength));

    vec3 viewDir = normalize(viewPos - FragPos);

    vec3 diffuseTex  = texture(material.diffuse,  TexCoords).rgb;
    vec3 specularTex = texture(material.specular, TexCoords).rgb;
    vec3 emissionTex = texture(material.emission, TexCoords).rgb;

    vec4 fragPosLightSpace = lightSpaceMatrix * vec4(FragPos, 1.0);
    float shadow = calculateShadow(fragPosLightSpace);

    // Emission
    vec3 result = emissionTex;

    if (numLights == 0) {
        result += diffuseTex * 0.7;
    } else {
        for (int i = 0; i < numLights; ++i) {

            Light light = lights[i];

            vec3 lightDir;
            if (light.type == 0)
                lightDir = normalize(-light.direction);
            else
                lightDir = normalize(light.position - FragPos);

            // Ambient
            vec3 ambient = light.color * light.ambient * diffuseTex;

            // Diffuse
            float diff = max(dot(normal, lightDir), 0.0);
            vec3 diffuse = light.color * light.intensity * diff * diffuseTex;

            // Specular
            float shininess = max(material.shininess, 1.0);
            vec3 reflectDir = reflect(-lightDir, normal);
            float spec = pow(max(dot(viewDir, reflectDir), 0.0), shininess);
            vec3 specular = light.color * light.intensity * spec * specularTex;

            // Attenuation
            float attenuation = 1.0;
            if (light.type == 1 || light.type == 2) {
                float dist = length(light.position - FragPos);
                attenuation = 1.0 /
                    (light.constant + light.linear * dist + light.quadratic * (dist * dist));
            }

            // Spotlight
            float spotFactor = 1.0;
            if (light.type == 2) {
                float theta = dot(lightDir, normalize(-light.direction));
                float epsilon = max(light.cutOff - light.outerCutOff, 1e-5);
                spotFactor = clamp((theta - light.outerCutOff) / epsilon, 0.0, 1.0);
            }

            // Sombras só em directional light
            float shadowFactor = (light.type == 0) ? shadow : 0.0;

            vec3 lightResult =
                ambient +
                (1.0 - shadowFactor) * diffuse +
                (1.0 - shadowFactor * 0.5) * specular;

            lightResult *= attenuation * spotFactor;

            result += lightResult;
        }
    }

    FragColor = vec4(result, 1.0);
}
